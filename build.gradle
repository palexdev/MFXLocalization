plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version '0.0.12'
    id 'com.vanniktech.maven.publish' version '0.19.0'
}

group 'io.github.palexdev'
version '11.0.1'

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = "$jdk"
    targetCompatibility = "$jdk"
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit"
}

test {
    useJUnitPlatform()
}

javafx {
    version = "$jfx"
    modules = ['javafx.graphics']
}

javadoc {
    excludes = ['**/*.html', 'META-INF/**']

    options.use = true
    options.splitIndex = true
    options.encoding = 'UTF-8'
    options.author = true
    options.version = true
    options.windowTitle = "$project.name $project.version API"
    options.docTitle = "$project.name $project.version API"
    options.links = ['https://docs.oracle.com/en/java/javase/11/docs/api',
                     'https://openjfx.io/javadoc/18']
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJarBuild(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

jar {
    manifest {
        attributes(
                'Bundle-Name': project.name,
                'Bundle-Description': "I18N bundles for MaterialFX and its dependencies",
                'Bundle-SymbolicName': 'io.github.palexdev',
                'Export-Package': 'io.github.palexdev.mfxlocalization'
        )
    }
}

artifacts {
    archives javadocJar
    archives sourcesJarBuild
    archives jar
}

mavenPublish {
    sonatypeHost = "S01"
}